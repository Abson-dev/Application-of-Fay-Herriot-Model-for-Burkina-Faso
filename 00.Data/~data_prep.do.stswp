//do file to prepare SURVEY data for poverty mapping
*same as collapse




*ssc install groupfunction
/*
cap: net install github, from("https://haghish.github.io/github/")
github install jpazvd/fhsae
github install pcorralrodas/groupfunction 
github install pcorralrodas/sp_groupfunction

*/

**************************************
/*
we calculate direct estimates at every geographic level of disaggregation: national, regional, admin level 1, admin level 2, and admin level 3. We only use the output from 01_dataprep_survey.do.
To obtain direct estimates with correctly calculated standard errors from a complex sample, it is necessary to input the structure by specifying the stratification and clustering used ( svyset in Stata). The script describes how to obtain direct estimates of poverty rates and their sampling variances for each area in the sample. 
*/
*****************************************
clear all

set more off

version 14





*===============================================================================
//Specify team paths 
*===============================================================================

global main          "C:\Users\AHema\OneDrive - CGIAR\Desktop\Poverty Mapping\Small area estimation\Burkina Faso\Application of Fay-Herriot Model for Burkina Faso"
global data       	"$main\00.Data"

use "$data\survey_ehcvm_bfa_2021.dta",clear

rename milieu1 urban
rename pcexp welfare
rename zref pl_abs
rename hhweight2021 WTA_S_HHSIZE
rename province district
rename grappe clust //to check


egen strata = group(region urban)
svyset clust [pw=WTA_S_HHSIZE], strata(strata)
gen fgt0 = (welfare < pl_abs) if !missing(welfare)

preserve
groupfunction [aw=WTA_S_HHSIZE], mean(fgt0) rawsum(WTA_S_HHSIZE) by(district clust)

groupfunction [aw=WTA_S_HHSIZE], mean(fgt0) count(clust) by(district)
restore


preserve
//The below are for comparison to final FH estimates
svy: proportion fgt0, over(region)
	mata: fgt0 = st_matrix("e(b)")
	mata: fgt0 = fgt0[(cols(fgt0)/2+1)..cols(fgt0)]'
	mata: fgt0_var = st_matrix("e(V)")
	mata: fgt0_var = diagonal(fgt0_var)[(cols(fgt0_var)/2+1)..cols(fgt0_var)]

	groupfunction [aw=WTA_S_HHSIZE], mean(fgt0) rawsum(WTA_S_HHSIZE) by(region)
	sort region
	getmata dir_fgt0 = fgt0 dir_fgt0_var = fgt0_var

	replace dir_fgt0_var = . if dir_fgt0_var==0
	replace dir_fgt0 = . if missing(dir_fgt0_var)

	save "$data\direct_survey_ehcvm_bfa_2021_region.dta", replace


restore

svy:proportion fgt0, over(district)

mata: fgt0 = st_matrix("e(b)")
mata: fgt0 = fgt0[(cols(fgt0)/2+1)..cols(fgt0)]'
mata: fgt0_var = st_matrix("e(V)")
mata: fgt0_var = diagonal(fgt0_var)[(cols(fgt0_var)/2+1)..cols(fgt0_var)]

gen N=1 //Need the number of observation by district...for smoother variance function
gen N_hhsize = hhsize

//Number of EA by district
bysort district clust: gen num_ea = 1 if _n==1

groupfunction [aw=WTA_S_HHSIZE], mean(fgt0) rawsum(N WTA_S_HHSIZE N_hhsize num_ea) by(region district)

sort district
getmata dir_fgt0 = fgt0 dir_fgt0_var = fgt0_var

gen zero = dir_fgt0 //original variable with direct estimates

replace dir_fgt0_var = . if dir_fgt0_var==0
replace dir_fgt0 = . if missing(dir_fgt0_var)

save "$data\direct_survey_ehcvm_bfa_2021.dta", replace